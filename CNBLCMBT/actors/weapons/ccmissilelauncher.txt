/*

Weapon - Missile Launcher

A large tubed missile weapon. Fires missiles.

*/

actor ccMissileLauncher : ccBallistic replaces RocketLauncher {
	// Text Info
	Tag "$WT_MISSILELAUNCHER"
	//Obituary ""
	
	// Sounds
	//Weapon.UpSound "sound"
	//Weapon.ReadySound "sound"
	//AttackSound "weapons/dspistol"

	// States
	States {
		// Weapon Selection
		/*
		This is the set of startup actions when selecting the weapon.
		*/
		Select:
			Goto StartSelect
		PrepWeapon:
			// Apply weapon's weight.
			TNT1 A 0 A_GiveInventory("WeaponWeight", 45)
			// Swapping from another weapon to this weapon.
			TNT1 A 1 A_JumpIfInventory("ccMissileLauncher_Missile_m", 1, "PrepMode.Missile")
			// If the player never had the gun yet, give this weapon's default mode.
			MLAU A 0 A_GiveInventory("ccMissileLauncher_Missile", 1)
			Goto CheckWeaponChamber
		// Mode Preparation
		/*
		This turns the weapon's mode in memory into the active weapon mode on the HUD.
		*/
		PrepMode.Missile:
			TNT1 A 0 A_TakeInventory("ccMissileLauncher_Missile_m", 1)
			TNT1 A 0 A_GiveInventory("ccMissileLauncher_Missile", 1)
			Goto CheckWeaponChamber
		// Ammo Loading
		/*
		This brings all the ammo from both the weapon itself and the type,
		into the "current weapon" and "current reserve" slots to read how
		much ammo is available for the gun.
		*/
		CheckWeaponChamber:
			TNT1 A 0 A_JumpIfInventory("ccMissileLauncher_Chamber", 1, "LoadChamber")
			Goto CheckWeaponReserve
		LoadChamber:
			TNT1 A 0 A_TakeInventory("ccMissileLauncher_Chamber", 1)
			TNT1 A 0 A_GiveInventory("Current_Chamber", 1)
			TNT1 A 0 A_JumpIfInventory("ccMissileLauncher_Chamber", 1, "LoadChamber")
			Goto CheckWeaponChamber
		CheckWeaponReserve:
			MLAU A 0 A_JumpIfInventory("missile_", 1, "LoadReserve")
			Goto PostSelect
		LoadReserve:
			TNT1 A 0 A_TakeInventory("missile_", 1)
			TNT1 A 0 A_GiveInventory("Current_Reserve", 1)
			TNT1 A 0 A_JumpIfInventory("missile_", 1, "LoadReserve")
			Goto CheckWeaponReserve
		// Active Weapon
		/*
		The weapon is now active. Run the routines necessary.
		*/
		Ready:
			MLAU A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
			Goto CheckReady
		AmmoReady:
			MLAU A 0 A_JumpIfInventory("Current_Chamber", 2, "LimitChamber")
			MLAU A 0 A_JumpIfInventory("Current_Reserve", 21, "LimitReserve")
			MLAU A 0 A_JumpIfInventory("missile_", 1, "PushReserve")
			Goto Ready
		// Push Reserve Ammo
		/*
		When collecting ammo for this active weapon, make sure all of its
		related ammo in reserve gets pushed to the "current reserve" so the
		player can see how much ammo total of that type.
		*/
		PushReserve:
			TNT1 A 0 A_TakeInventory("missile_", 1)
			Goto InsertReserve
		// Deactivate Weapon
		/*
		When the user puts down the weapon for another, run the routines that
		pack away all the ammo appropriately, set the weapon mode to memory,
		and finally end use of the weapon. This works like a reverse of
		bringing the weapon up.
		*/
		Deselect:
			Goto CheckCurrentMode
		CheckCurrentMode:
			TNT1 A 0 A_JumpIfInventory("ccMissileLauncher_Missile", 1, "UndoMode.Missile")
			Stop
		// Mode Memory
		/*
		Remember the mode the gun had before putting away, and keep the tag
		name out of the HUD.
		*/
		UndoMode.Missile:
			TNT1 A 0 A_TakeInventory("ccMissileLauncher_Missile", 1)
			TNT1 A 0 A_GiveInventory("ccMissileLauncher_Missile_m", 1)
			Goto CheckCurrentChamber
		// Unload Ammo
		/*
		Put the ammo out of the "current weapon" into the weapon's own chamber,
		and the reserve ammo back to its storage.
		*/
		UnloadChamber:
			TNT1 A 0 A_TakeInventory("Current_Chamber", 1)
			TNT1 A 0 A_GiveInventory("ccMissileLauncher_Chamber", 1)
			TNT1 A 0 A_JumpIfInventory("Current_Chamber", 1, "UnloadChamber")
			Goto CheckCurrentChamber
		UnloadReserve:
			TNT1 A 0 A_TakeInventory("Current_Reserve", 1)
			TNT1 A 0 A_GiveInventory("missile_", 1)
			MLAU A 0 A_JumpIfInventory("Current_Reserve", 1, "UnloadReserve")
			Goto PostDeselect
			
		// Weapon Aim/Zoom
		Zoom:
			Goto Zoom.ZNX
			
		// Toggle Firing Modes
		AltFire:
			Stop
			
		// Firing Mode Actions
		Fire:
			TNT1 A 0 A_JumpIfNoAmmo("DryFire")
		Fire.Missile:
			MLAU B 1 A_GunFlash
			MLAU A 0 A_FireCustomMissile("missile_p")
			Goto Fire.Missile.Recoil
		Fire.Missile.Recoil:
			MLAU A 0 A_SpawnItemEx("gunsmoke", 20, 0, 15, 1, 0, 0.5, 0)
			MLAU A 1 Offset(0,38) A_SetPitch(pitch-2)
			MLAU A 1 Offset(0,35)
			MLAU A 4 A_SetPitch(pitch+0.5)
			Goto Ready
			
		// Weapon Reload
		Reload:
			MLAU A 0 A_JumpIfInventory("Current_Chamber", 1, "Ready")
			MLAU A 0 A_JumpIfInventory("Current_Reserve", 1, "StartReload")
			Goto Ready
		StartReload:
			Goto Reload.WeaponDown
		LoopReload:
			TNT1 A 0 A_TakeInventory("Current_Reserve", 1)
			TNT1 A 0 A_GiveInventory("Current_Chamber", 1)
			TNT1 A 0 A_JumpIfInventory("Current_Chamber", 1, "FinishReload")
			TNT1 A 0 A_JumpIfInventory("Current_Reserve", 1, "LoopReload")
			Goto FinishReload
		FinishReload:
			TNT1 A 11
			MLAU A 0
			Goto Reload.WeaponUp
	}
}



// Weapon Modes
/*
These are abstract actors native to the weapon, to allow multiple
firing modes.
*/
actor ccMissileLauncher_Missile : Inventory {
	Tag "$WMT_MISSILELAUNCHER_MISSILE"
	Inventory.MaxAmount 1
	+INVENTORY.INVBAR
}



// Weapon Modes - Memory
/*
These are here so the system "remembers" what setting the gun had,
without writing the Tag on the HUD.
*/
actor ccMissileLauncher_Missile_m : Inventory {
	Inventory.MaxAmount 1
	+INVENTORY.PERSISTENTPOWER
}



// Weapon Chamber
/*
This is the ammunition inside the gun, so the game remembers how
much was in it when pulling it out again.
*/
actor ccMissileLauncher_Chamber : Ammo {
	Inventory.MaxAmount 1
}





// Weapon-Swap Spawn
/*
This is the spawn object of the weapon.
*/
actor ccMissileLauncherPickup : ccWeaponPickup 11303 {
	/*
	This is the amount of ammo the pickup provides when picked up.
	*/
	Accuracy 2
	
	/*
	Identifies this actor class and derived classes as the named pickup.
	This property needs to match the actor class's name.
	*/
	Species "ccMissileLauncherPickup"

	/*
	This is the name printed on the HUD when using pickup.
	It also displays in the pickup message.
	*/
	Tag "$WT_MISSILELAUNCHER"

	States {
		Spawn:
			MLAU Z 0
			Goto Init
	}
}