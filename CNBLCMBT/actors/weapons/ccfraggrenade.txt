/*

Weapon - Frag Grenade

A basic hand grenade.

*/

actor ccFragGrenade : ccThrowable {
	// Text Info
	Tag "Frag Grenade"	
	//Obituary ""
	
	// Sounds
	//Weapon.UpSound "sound"
	//Weapon.ReadySound "sound"
	//AttackSound "weapons/dspistol"

	// States
	States {
		// Weapon Selection
		/*
		This is the set of startup actions when selecting the weapon.
		*/
		Select:
			Goto StartSelect
		PrepWeapon:
			// Apply weapon's weight.
			TNT1 A 0 A_GiveInventory("WeaponWeight", 5)
			// Swapping from another weapon to this weapon.
			TNT1 A 0 A_JumpIfInventory("ccFragGrenade_Normal_m", 1, "PrepMode.Normal")
			// Starting a map with this weapon.
			TNT1 A 0 A_JumpIfInventory("ccFragGrenade_Normal", 1, "CheckWeaponChamber")
			// If the player never had the gun yet, give this weapon's default mode.
			GFRA A 0 A_GiveInventory("ccFragGrenade_Normal", 1)
			Goto CheckWeaponChamber
		// Mode Preparation
		/*
		This turns the weapon's mode in memory into the active weapon mode on the HUD.
		*/
		PrepMode.Normal:
			GFRA A 0 A_TakeInventory("ccFragGrenade_Normal_m", 1)
			GFRA A 0 A_GiveInventory("ccFragGrenade_Normal", 1)
			Goto CheckWeaponChamber
		// Ammo Loading
		/*
		This brings all the ammo from both the weapon itself and the type,
		into the "current weapon" and "current reserve" slots to read how
		much ammo is available for the gun.
		*/
		CheckWeaponChamber:
			"####" "#" 0 A_JumpIfInventory("ccFragGrenade_Chamber", 1, "LoadChamber")
			Goto CheckWeaponReserve
		LoadChamber:
			"####" "#" 0 A_TakeInventory("ccFragGrenade_Chamber", 1)
			"####" "#" 0 A_GiveInventory("Current_Chamber", 1)
			"####" "#" 0 A_JumpIfInventory("ccFragGrenade_Chamber", 1, "LoadChamber")
			Goto CheckWeaponChamber
		CheckWeaponReserve:
			"####" "#" 0 A_JumpIfInventory("grenade_frag", 1, "LoadReserve")
			Goto PostSelect
		LoadReserve:
			"####" "#" 0 A_TakeInventory("grenade_frag", 1)
			"####" "#" 0 A_GiveInventory("Current_Reserve", 1)
			"####" "#" 0 A_JumpIfInventory("grenade_frag", 1, "LoadReserve")
			Goto CheckWeaponReserve
		// Active Weapon
		/*
		The weapon is now active. Run the routines necessary.
		*/
		Ready:
			GFRA A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
			Goto CheckReady
		AmmoReady:
			"####" "#" 0 A_JumpIfInventory("Current_Chamber", 2, "LimitChamber")
			"####" "#" 0 A_JumpIfInventory("Current_Reserve", 9, "LimitReserve")
			"####" "#" 0 A_JumpIfInventory("grenade_frag", 1, "PushReserve")
			Goto Ready
		// Push Reserve Ammo
		/*
		When collecting ammo for this active weapon, make sure all of its
		related ammo in reserve gets pushed to the "current reserve" so the
		player can see how much ammo total of that type.
		*/
		PushReserve:
			TNT1 A 0 A_TakeInventory("grenade_frag", 1)
			Goto InsertReserve
		// Deactivate Weapon
		/*
		When the user puts down the weapon for another, run the routines that
		pack away all the ammo appropriately, set the weapon mode to memory,
		and finally end use of the weapon. This works like a reverse of
		bringing the weapon up.
		*/
		Deselect:
			GFRA A 0 A_JumpIfInventory("ccFragGrenade_Normal", 1, "UndoMode.Normal")
			Stop
		// Mode Memory
		/*
		Remember the mode the gun had before putting away, and keep the tag
		name out of the HUD.
		*/
		UndoMode.Normal:
			GFRA A 0 A_TakeInventory("ccFragGrenade_Normal", 1)
			GFRA A 0 A_GiveInventory("ccFragGrenade_Normal_m", 1)
			Goto CheckCurrentChamber
		// Unload Ammo
		/*
		Put the ammo out of the "current weapon" into the weapon's own chamber,
		and the reserve ammo back to its storage.
		*/
		UnloadChamber:
			"####" "#" 0 A_TakeInventory("Current_Chamber", 1)
			"####" "#" 0 A_GiveInventory("ccFragGrenade_Chamber", 1)
			"####" "#" 0 A_JumpIfInventory("Current_Chamber", 1, "UnloadChamber")
			Goto CheckCurrentChamber
		UnloadReserve:
			"####" "#" 0 A_TakeInventory("Current_Reserve", 1)
			"####" "#" 0 A_GiveInventory("grenade_frag", 1)
			"####" "#" 0 A_JumpIfInventory("Current_Reserve", 1, "UnloadReserve")
			Goto PostDeselect
			
		// Weapon Aim/Zoom
		Zoom:
			//Put a zoom frame here before going to the zoom.
			Goto Zoom.ZNX
			
		// Firing Mode Actions
		Fire:
			GFRA A 1 Offset(0, 35)
			GFRA A 1 Offset(0, 38)
			GFRA A 1 Offset(0, 45)
			GFRA A 1 Offset(0, 55)
			TNT1 A 10
			//GFRA A 0 A_SpawnItemEx("grenade_frag_p", 0, 0, 0, 15, 0, 10)
			GFRA A 0 A_FireCustomMissile("grenade_frag_p", FRandom(-0.5, 0.5), true, 0, 0, false, FRandom(-0.5, 0.5))
			GFRA A 0 A_TakeInventory("Current_Chamber", 1)
			GFRA A 0 A_JumpIfInventory("Current_Reserve", 1, "PullNew")
			Goto DiscardWeapon
		PullNew:
			TNT1 A 25
			GFRA A 0 A_TakeInventory("Current_Reserve", 1)
			GFRA A 0 A_GiveInventory("Current_Chamber", 1)
			GFRA A 1 Offset(0, 55)
			GFRA A 1 Offset(0, 45)
			GFRA A 1 Offset(0, 38)
			GFRA A 1 Offset(0, 35)
			Goto Ready
		DiscardWeapon:
			GFRA A 0 A_TakeInventory("ccFragGrenade")
			Goto Deselect
	}
}



// Weapon Modes
/*
These are abstract actors native to the weapon, to allow multiple
firing modes.
*/
actor ccFragGrenade_Normal : Inventory {
	Tag "Normal"
	Inventory.MaxAmount 1
	+INVENTORY.INVBAR
}



// Weapon Modes - Memory
/*
These are here so the system "remembers" what setting the gun had,
without writing the Tag on the HUD.
*/
actor ccFragGrenade_Normal_m : Inventory {
	Inventory.MaxAmount 1
	+INVENTORY.PERSISTENTPOWER
}



// Weapon Chamber
/*
This is the ammunition inside the weapon, so the game remembers how
much was in it when pulling it out again.
*/
actor ccFragGrenade_Chamber : Ammo {
	Inventory.MaxAmount 1
}





// Weapon-Swap Spawn
/*
This is the spawn object of the weapon.
*/
actor ccFragGrenadePickup : ccWeaponPickup 11700 {
	/*
	This is the amount of ammo the pickup provides when picked up.
	*/
	Accuracy 8
	
	/*
	Identifies this actor class and derived classes as the named pickup.
	This property needs to match the actor class's name.
	*/
	Species "ccFragGrenadePickup"

	/*
	This is the name printed on the HUD when using pickup.
	It also displays in the pickup message.
	*/
	Tag "FragGrenade_pickup"

	States {
		Spawn:
			TNT2 A 0
			Goto Init
	}
}